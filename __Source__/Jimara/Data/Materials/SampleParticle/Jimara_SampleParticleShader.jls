// Fragment shader input:
struct Jimara_FragmentInput {
	vec3 position;
	vec3 normal;
	vec2 uv;
	vec4 color;
};

/** ############################################ VERTEX SHADER: ############################################ */
#ifdef JIMARA_VERTEX_SHADER

// Vertex input:
layout(location = 0) in vec3 vertPosition;
layout(location = 1) in vec3 vertNormal;
layout(location = 2) in vec2 vertUV;

// Instance input:
layout(location = 3) in mat4 localTransform;
layout(location = 7) in vec4 partileColor;
layout(location = 8) in vec4 particleTilingAndOffset;

Jimara_FragmentInput Jimara_CalculateFragmentInput() {
	vec4 position = localTransform * vec4(vertPosition, 1.0f);
	Jimara_FragmentInput fragment;
	fragment.position = (position).xyz;
	fragment.normal = (localTransform * vec4(vertNormal, 0.0f)).xyz;
	fragment.uv = vertUV * particleTilingAndOffset.xy + particleTilingAndOffset.zw;
	fragment.color = partileColor;
	return fragment;
}


#else
/** ########################################### FRAGMENT SHADER: ########################################### */
#ifdef JIMARA_FRAGMENT_SHADER

// Texture sampler:
layout(set = MATERIAL_BINDING_SET_ID, binding = 0) uniform MaterialSettings {
	vec3 baseColor;
	float alphaThreshold;
} materialSettings;
layout(set = MATERIAL_BINDING_SET_ID, binding = 1) uniform sampler2D texSampler;

// Gbuffer:
struct Jimara_GeometryBuffer {
	vec3 position;
	vec3 normal;
	vec3 color;
	vec4 pixelColor;
};

Jimara_GeometryBuffer Jimara_BuildGeometryBuffer(in Jimara_FragmentInput fragment) {
	vec4 color = texture(texSampler, fragment.uv);
	if (color.a < materialSettings.alphaThreshold) discard;
	color *= fragment.color;
	return Jimara_GeometryBuffer(
		fragment.position, 
		normalize(fragment.normal), 
		color.rgb * materialSettings.baseColor,
		vec4(0.0, 0.0, 0.0, color.a));
}

void Jimara_IlluminateFragment(in Photon photon, inout Jimara_GeometryBuffer gbuffer) {
	gbuffer.pixelColor.rgb += gbuffer.color * photon.color;
}

#endif
#endif
