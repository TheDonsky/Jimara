// Expose Editor name and discription:
#pragma JM_MaterialPath(name = "Sample Diffuse", path = "Jimara/Sample Diffuse", hint = "Simple diffuse shader, serving as a concrete example of API usage");

// Sample material is opaque; refer to the JLS_Template.jls.sample for further details about available options;
#pragma JM_BlendMode JM_Blend_Opaque

// No optional flags are enabled; this is not mandatory; refer to the JLS_Template.jls.sample for further details about available options;
#pragma JM_MaterialFlags 0

// Parameters:
#pragma JM_MaterialProperty vec3 baseColor;
#pragma JM_MaterialProperty sampler2D colorTexture;
#pragma JM_MaterialProperty sampler2D normalMap;

// Fragment data:
#pragma JM_FragmentField vec3 normal;
#pragma JM_FragmentField vec3 uv;

// Fragment shader input:
struct Jimara_FragmentInput {
	vec3 position;
	vec3 normal;
	vec2 uv;
};

/** ############################################ VERTEX SHADER: ############################################ */
#ifdef JIMARA_VERTEX_SHADER

// Vertex input:
layout(location = 0) in vec3 vertPosition;
layout(location = 1) in vec3 vertNormal;
layout(location = 2) in vec2 vertUV;

// Instance input:
layout(location = 3) in mat4 localTransform;

Jimara_FragmentInput Jimara_CalculateFragmentInput() {
	vec4 position = localTransform * vec4(vertPosition, 1.0f);
	Jimara_FragmentInput fragment;
	fragment.position = (position).xyz;
	fragment.normal = (localTransform * vec4(vertNormal, 0.0f)).xyz;
	fragment.uv = vertUV;
	return fragment;
}


#else
/** ########################################### FRAGMENT SHADER: ########################################### */
#ifdef JIMARA_FRAGMENT_SHADER
#include "../NormalMap.glh"

// Texture sampler:
layout(set = MATERIAL_BINDING_SET_ID, binding = 0) uniform BaseColor {
	vec3 value;
} baseColor;
layout(set = MATERIAL_BINDING_SET_ID, binding = 1) uniform sampler2D texSampler;
layout(set = MATERIAL_BINDING_SET_ID, binding = 2) uniform sampler2D normalMap;

// Gbuffer:
struct Jimara_GeometryBuffer {
	vec3 position;
	vec3 normal;
	float roughness;
	vec3 color;
	vec4 pixelColor;
};

Jimara_GeometryBuffer Jimara_BuildGeometryBuffer(in Jimara_FragmentInput fragment) {
	return Jimara_GeometryBuffer(
		fragment.position, 
		Jimara_SampleNormalMap_Linear(normalMap, fragment.normal, fragment.position, fragment.uv),
		1.0,
		texture(texSampler, fragment.uv).rgb * baseColor.value,
		vec4(0.0, 0.0, 0.0, 1.0));
}

void Jimara_IlluminateFragment(in Photon photon, inout Jimara_GeometryBuffer gbuffer) {
	vec3 direction = normalize(photon.origin - gbuffer.position);
	float tangent = max(dot(direction, gbuffer.normal), 0.0);
	gbuffer.pixelColor.rgb += tangent * gbuffer.color * photon.color;
}

#define JIMARA_OPAQUE_LIT_SHADER

#endif
#endif
