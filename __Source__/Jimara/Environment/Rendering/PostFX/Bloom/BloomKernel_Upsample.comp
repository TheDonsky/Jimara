#version 450
#extension GL_EXT_shader_image_load_formatted: require
#define BLOCK_SIZE 16
layout(local_size_x = BLOCK_SIZE, local_size_y = BLOCK_SIZE, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform sampler2D source;
layout(set = 0, binding = 1) uniform image2D result;
layout(set = 0, binding = 2) uniform Params {
    float baseWeight;
    float bloomWeight;
} params;

void main() {
    ivec2 coord = ivec2(int(gl_GlobalInvocationID.x), int(gl_GlobalInvocationID.y));
    ivec2 resolution = imageSize(result).xy;

    if (coord.x >= resolution.x || coord.y >= resolution.y) return;

    vec2 uvStep = vec2(
        1.0 / float(resolution.x), 
        1.0 / float(resolution.y));

    vec2 uv = vec2(
		uvStep.x * (float(coord.x) + 0.5), 
		uvStep.y * (float(coord.y) + 0.5));

    uvStep *= 2.0;

    const float CORNER = 0.0625;    // 1 / 16;
    const float CROSS = 0.125;      // 2 / 16;
    const float CENTER = 0.25;      // 4 / 16;

    vec4 color = texture(source, uv) * CENTER;

    color += (
        texture(source, uvStep * vec2(-1.0, -1.0) + uv) +
        texture(source, uvStep * vec2(1.0, -1.0) + uv) +
        texture(source, uvStep * vec2(-1.0, 1.0) + uv) +
        texture(source, uvStep * vec2(1.0, 1.0) + uv)) * CORNER;

    color += (
        texture(source, uvStep * vec2(-1.0, 0.0) + uv) +
        texture(source, uvStep * vec2(1.0, 0.0) + uv) +
        texture(source, uvStep * vec2(0.0, -1.0) + uv) +
        texture(source, uvStep * vec2(0.0, 1.0) + uv)) * CROSS;

    vec4 baseColor = imageLoad(result, coord);
    vec3 mixRGB = baseColor.rgb * params.baseWeight + color.rgb * params.bloomWeight;
    imageStore(result, coord, vec4(mixRGB, baseColor.w));
}
