
struct Vertex {
	vec3 position;
	float pad0;
	vec3 normal;
	float pad1;
	vec2 uv;
	vec2 pad2;
};

layout(buffer_reference) buffer Vertices { Vertex data[]; };
layout(buffer_reference) buffer Indices { uint data[]; };

struct BufferRefs {
	uint64_t verts;
	uint64_t indices;
};
layout (set = 0, binding = 3) readonly buffer ObjectBuffers { 
	BufferRefs data[]; 
} objectBuffers;

layout (set = 0, binding = 1) uniform accelerationStructureEXT accelerationStructure;

hitAttributeEXT vec2 attribs;
struct Payload {
	vec3 color;
	vec3 reflectionDir;
	vec3 hitPoint;
};
layout(location = 0) rayPayloadInEXT Payload prd;

struct SurfaceIlluminationInfo {
	vec3 position;
	vec3 lightDir;
	vec3 lightColor;
};
layout(location = 0) callableDataEXT SurfaceIlluminationInfo light;

#ifndef RT_NO_RECURSE
layout(location = 1) rayPayloadEXT uint missed;
#endif

void Illuminate(in vec3 normal) {
	const vec3 midDir = -normalize(gl_WorldRayDirectionEXT + light.lightDir);
	const float cosine = dot(midDir, normal);
	prd.color += cosine * light.lightColor;
}

void CastLightRay() {
#ifndef RT_NO_RECURSE
	missed = 0;
	traceRayEXT(
		accelerationStructure,
		gl_RayFlagsSkipClosestHitShaderEXT,
		0xFF,					// Cull Mask
		0,						// SBT record offset
		0, 						// SBT record stride
		1,		 				// Miss index
		light.position - 100.0 * light.lightDir,	// Origin
		0.01,										// minT
		light.lightDir,								// Direction
		99.9,										// maxT
		1						// Payload (location = 1)
		);
	if (missed == 0)
		light.lightColor = vec3(0.0);
#endif
}

void main() {
	const BufferRefs bufferRefs = objectBuffers.data[gl_InstanceID];
	Vertices vertices = Vertices(bufferRefs.verts);
	Indices indices = Indices(bufferRefs.indices);

	const uvec3 tri = uvec3(
		indices.data[gl_PrimitiveID * 3 + 0],
		indices.data[gl_PrimitiveID * 3 + 1],
		indices.data[gl_PrimitiveID * 3 + 2]);
	const Vertex a = vertices.data[tri.x];
	const Vertex b = vertices.data[tri.y];
	const Vertex c = vertices.data[tri.z];

	const vec3 barycentrics = vec3(1.0 - attribs.x - attribs.y, attribs.x, attribs.y);

	const mat4x3 pose = gl_ObjectToWorldEXT;

	mat4x4 transform;
	transform[0] = vec4(pose[0], 0.0);
	transform[1] = vec4(pose[1], 0.0);
	transform[2] = vec4(pose[2], 0.0);
	transform[3] = vec4(pose[3], 1.0);

	const vec3 position = (transform * vec4(
		barycentrics.x * a.position +
		barycentrics.y * b.position + 
		barycentrics.z * c.position, 1.0)).xyz;
	const vec3 normal = normalize((transform * vec4(
		barycentrics.x * a.normal +
		barycentrics.y * b.normal + 
		barycentrics.z * c.normal, 0.0)).xyz);

	const vec3 rayDir = -gl_WorldRayDirectionEXT;
	const float cosine = dot(normal, rayDir);
	const vec3 diff = rayDir - normal * cosine;
	const vec3 reflDir = rayDir - 2.0 * diff;

	prd.color = vec3(0.0);
	light.position = position;
	executeCallableEXT(0, 0);
	Illuminate(normal);
	executeCallableEXT(1, 0);
	CastLightRay();
	Illuminate(normal);

	prd.reflectionDir = 0.75 * cosine * reflDir;
	prd.hitPoint = position;
}
